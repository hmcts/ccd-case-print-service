plugins {
  id 'java'
  id 'application'
  id 'com.github.spacialcircumstances.gradle-cucumber-reporting' version '0.1.14'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
  maven {
    url 'https://dl.bintray.com/hmcts/hmcts-maven'
  }
}

dependencies {
  testCompile group: 'uk.gov.hmcts', name: 'befta-fw', version: '6.0.rdm.8934.2'
}

sourceSets {
  aat {
    java {
      srcDir('src/java')
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
    }
    resources {
      srcDir('src/resources')
    }
  }
}

configurations {
  aatCompile.extendsFrom(testCompile)
  cucumberRuntime.extendsFrom(functionalRuntime)
}

task smoke(type: Test) {
  description = 'Executes smoke tests against an the CCD Case Print Service API instance just deployed'
  setTestClassesDirs(sourceSets.aat.output.classesDirs)

  doFirst {
    generateCucumberReports.enabled = true
    javaexec {
      main = "uk.gov.hmcts.ccd.printservice.befta.CasePrintServiceBeftaMain"
      classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
      args = ['--plugin', "json:${rootDir}/target/cucumber.json", '--tags', '@Smoke', '--glue',
              'uk.gov.hmcts.befta.player', 'src/resources/features']
    }
  }

  finalizedBy {
    generateCucumberReports {
      doLast{
        new File("${rootDir}/BEFTA Report for Smoke Tests").mkdirs()
        file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Smoke Tests"))
      }
    }
  }

  outputs.upToDateWhen { false }
}

task functional(type: Test) {
  description = 'Executes functional tests against an the CCD Case Print Service API instance just deployed'
  setTestClassesDirs(sourceSets.aat.output.classesDirs)

  doFirst {
    generateCucumberReports.enabled = true
    javaexec {
      main = "uk.gov.hmcts.ccd.printservice.befta.CasePrintServiceBeftaMain"
      classpath += configurations.cucumberRuntime + sourceSets.aat.runtimeClasspath + sourceSets.main.output + sourceSets.test.output
      args = ['--plugin', "json:${rootDir}/target/cucumber.json", '--tags', 'not @Ignore', '--glue',
              'uk.gov.hmcts.befta.player', 'src/resources/features']
    }
  }

  finalizedBy {
    generateCucumberReports {
      doLast{
        new File("${rootDir}/BEFTA Report for Functional Tests").mkdirs()
        file("${rootDir}/target/cucumber/cucumber-html-reports").renameTo(file("${rootDir}/BEFTA Report for Functional Tests"))
      }
    }
  }

  outputs.upToDateWhen { false }
}

cucumberReports {
  outputDir = file("${rootDir}/target/cucumber")
  reports = files("${rootDir}/target/cucumber.json")
}
